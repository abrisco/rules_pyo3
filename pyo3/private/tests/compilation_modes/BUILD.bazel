load("@bazel_skylib//rules:expand_template.bzl", "expand_template")
load("@rules_python//python:defs.bzl", "py_test")
load("//pyo3:defs.bzl", "pyo3_extension")

MODES = [
    "dbg",
    "opt",
    "fastbuild",
    "current",
]

[
    expand_template(
        name = "string_sum_{}_src".format(mode),
        out = "string_sum_{}.rs".format(mode),
        substitutions = {
            "fn string_sum_current(": "fn string_sum_{}(".format(mode),
        },
        template = "string_sum_current.rs",
    )
    for mode in MODES
    # `current` is the actual source file.
    if mode != "current"
]

[
    pyo3_extension(
        name = "string_sum_{}".format(mode),
        srcs = ["string_sum_{}.rs".format(mode)],
        compilation_mode = mode,
        edition = "2021",
    )
    for mode in MODES
]

[
    expand_template(
        name = "string_sum_{}_test_src".format(mode),
        out = "string_sum_{}_test.py".format(mode),
        substitutions = {
            "compilation_modes.string_sum_current": "compilation_modes.string_sum_{}".format(mode),
        },
        template = "string_sum_current_test.py",
    )
    for mode in MODES
    # `current` is the actual source file.
    if mode != "current"
]

[
    py_test(
        name = "string_sum_{}_test".format(mode),
        srcs = ["string_sum_{}_test.py".format(mode)],
        deps = [":string_sum_{}".format(mode)],
    )
    for mode in MODES
]
